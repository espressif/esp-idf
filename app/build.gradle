apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {

    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 5
        versionName "1.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "transport", "security"

    productFlavors {

        wifi {
            dimension "transport"
            applicationId "com.espressif.provsoftap"
        }
        ble {
            dimension "transport"
            applicationId "com.espressif.provble"
        }
        sec0 {
            dimension "security"
        }
        sec1 {
            dimension "security"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "proof_of_possesion", "abcd1234"
            resValue "string", "wifi_base_url", "192.168.4.1:80"
            resValue "string", "wifi_network_name_prefix", "PROV_"
            resValue "string", "ble_device_name_prefix", "PROV_"
            buildConfigField "boolean", "IS_ALLOWED_FILTERING_BY_PREFIX", "true"
        }

        debug {
            debuggable true
            resValue "string", "proof_of_possesion", "abcd1234"
            resValue "string", "wifi_base_url", "192.168.4.1:80"
            resValue "string", "wifi_network_name_prefix", "PROV_"
            resValue "string", "ble_device_name_prefix", "PROV_"
            buildConfigField "boolean", "IS_ALLOWED_FILTERING_BY_PREFIX", "true"
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.android.support:design:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    implementation 'com.google.crypto.tink:tink-android:1.1.0'
}
